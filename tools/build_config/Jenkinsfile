#!groovy

def get_matlab_release(String job_name) {
  return 'R' + job_name[-5..-1]
}

def get_build_type(String job_name) {
  if (job_name.startsWith('Release-')) {
    return 'Release'
  } else if (job_name.startsWith('Branch-')) {
    return 'Branch'
  } else if(job_name.startsWith('PR-')) {
    return 'Pull-request'
  } else {
    return 'Nightly'
  }
}

def get_agent(String job_name) {
  if (job_name.contains('Scientific-Linux-7')) {
    withCredentials([string(credentialsId: 'sl7_agent', variable: 'agent')]) {
      return "${agent}"
    }
  } else if (job_name.contains('Windows-10')) {
    withCredentials([string(credentialsId: 'win10_agent', variable: 'agent')]) {
      return "${agent}"
    }
  } else {
    return ''
  }
}

def get_release_type(String job_name) {
  String build_type = get_build_type(job_name);

  switch(build_type) {
    case 'Release':
      return 'release'

    case 'Pull-request':
      return 'pull_request'

    case 'Nightly':
      return 'nightly'

    default:
      return ''
  }
}

def get_branch_name(String job_name) {
  String build_type = get_build_type(job_name);

  switch(build_type) {
    case 'Nightly':
      return 'master'

    default:
      return ''
  }
}

String get_param(String param_name, String default_val) {
  // Return environment variable if present and non-empty
  // Else return default
  String value = "";
  try {
    value = env."${param_name}";
  } catch (groovy.lang.MissingPropertyException _) { }
  if (!value) {
    value = default_val;
  }
  println "${param_name} = ${value}"
  return value
}


def post_github_status(String state, String message) {
  // Non-PR builds will not set PR_STATUSES_URL - in which case we do not
  // want to post any statuses to Git
  if (env.PR_STATUSES_URL) {
    script {
      withCredentials([string(credentialsId: 'GitHub_API_Token',
                              variable: 'api_token')]) {
        if (isUnix()) {
          sh """
            curl -H "Authorization: token \${api_token}" \
              --request POST \
              --data '{"state": "${state}", \
                "description": "${message}", \
                "target_url": "$BUILD_URL", \
                "context": "$JOB_BASE_NAME"}' \
              $PR_STATUSES_URL > /dev/null
            """
        }
        else {
          powershell """
            [Net.ServicePointManager]::SecurityProtocol = "tls12, tls11, tls"
            \$payload = @{
              "state" = "${state}";
              "description" = "${message}";
              "target_url" = "$BUILD_URL";
              "context" = "$JOB_BASE_NAME"}

            Invoke-RestMethod -URI "$PR_STATUSES_URL" \
              -Headers @{Authorization = "token \$env:api_token"} \
              -Method 'POST' \
              -Body (\$payload|ConvertTo-JSON) \
              -ContentType "application/json"
          """
        }
      }
    }
  }
}

def write_git_revision_to_file(String file_name) {
  script {
    def git_rev_cmd = "git rev-parse HEAD"
    echo "Writing Git revision to ${file_name}..."
    if (isUnix()) {
      sh """
        echo "\$(${git_rev_cmd})" > ${file_name}
      """
    } else {
      powershell """
        Write-Output "\$(${git_rev_cmd})" > ${file_name}
      """
    }
  }
}

properties([
  parameters([
    string(
      defaultValue: get_branch_name(env.JOB_BASE_NAME),
      description: 'The name of the branch to build.',
      name: 'BRANCH_NAME',
      trim: true
    ),
    string(
      defaultValue: get_release_type(env.JOB_BASE_NAME),
      description: 'The type of the build e.g. "nightly", "release", "pull_request".',
      name: 'RELEASE_TYPE',
      trim: true
    ),
    string(
      defaultValue: get_agent(env.JOB_BASE_NAME),
      description: 'The agent to execute the pipeline on.',
      name: 'AGENT',
      trim: true
    ),
    string(
      description: 'The release number of the Matlab to load e.g. R2019b.',
      name: 'MATLAB_VERSION', trim: true
    ),
    string(
      description: 'The version of CMake to run the build with.',
      name: 'CMAKE_VERSION',
      trim: true
    ),
    string(
      description: 'The version of GCC to build with.',
      name: 'GCC_VERSION',
      trim: true
    ),
    string(
      description: 'The year of the version of Visual Studio to build with.',
      name: 'VS_VERSION',
      trim: true
    ),
    string(
      description: 'The version of CppCheck tooling to load to provide the code-style checks.',
      name: 'CPPCHECK_VERSION',
      trim: true
    )
  ])
])

if (env.BRANCH_NAME) {
  currentBuild.description = "Branch: ${env.BRANCH_NAME}"
} else if (env.PR_NUMBER) {
  currentBuild.displayName = "#${env.BUILD_NUMBER} PR-${env.PR_NUMBER}"
  currentBuild.description = "Git-SHA: ${env.PR_COMMIT_SHA.take(7)}"
}

Boolean run_build_stage = !(env?.PR_LABELS =~ "do-not-build")
Boolean run_test_stage = !(env?.PR_LABELS =~ "(do-not-build|do-not-test)")

pipeline {
  agent {
    label env.AGENT
  }

  environment {
    MATLAB_VERSION = get_param('MATLAB_VERSION', get_matlab_release(env.JOB_BASE_NAME))
    CMAKE_VERSION = get_param('CMAKE_VERSION', '3.19')
    GCC_VERSION = get_param('GCC_VERSION', '6.3.0')
    VS_VERSION = get_param('VS_VERSION', '2017')
    CPPCHECK_VERSION = get_param('CPPCHECK_VERSION', '1.77')
  }


  stages {

    stage('Notify') {
      steps {
        script {
          if (env.PR_LABELS) {
            echo "Found PR labels: ${env.PR_LABELS}"
          }
        }
        post_github_status("pending", "The build is running")
      }
    }

    stage('Configure') {
      steps {
        script {
          if (isUnix()) {
            sh '''
          export PATH=${PATH}:~/.local/bin &&
          module load cmake/\$CMAKE_VERSION &&
          module load matlab/\$MATLAB_VERSION &&
          module load gcc/\$GCC_VERSION &&
          module load cppcheck/\$CPPCHECK_VERSION &&
          ./tools/build_config/build.sh \
            --cmake_flags \"-DHorace_RELEASE_TYPE=\$RELEASE_TYPE\" \
            --matlab_release \$MATLAB_VERSION \
            --print_versions
            --configure
          '''
          } else {
            powershell '''
           ./tools/build_config/build.ps1 \
                -cmake_flags \"-DHorace_RELEASE_TYPE=\$env:RELEASE_TYPE\" \
                -matlab_release \$env:MATLAB_VERSION \
                -vs_version \$env:VS_VERSION \
                -print_versions \
                -configure
          '''
          }
        }
      }
    }

    stage('Analyze') {
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              module load matlab/\$MATLAB_VERSION &&
              module load gcc/\$GCC_VERSION &&
              module load cppcheck/\$CPPCHECK_VERSION &&
              ./tools/build_config/build.sh --analyze
            '''
          }
          else {
            powershell '''
              Write-Output \"Static Analysis in not performed on Windows\"
            '''
          }
        }
      }
    }

    stage('Build') {
      when {
        expression {
          run_build_stage == true
        }
      }
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              module load matlab/\$MATLAB_VERSION &&
              module load gcc/\$GCC_VERSION &&
              ./tools/build_config/build.sh --build
            '''
          }
          else {
            powershell '''
              ./tools/build_config/build.ps1 -build
            '''
          }
        }
      }
    }

    stage('Test') {
      when {
        expression {
          run_test_stage == true
        }
      }
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              module load matlab/\$MATLAB_VERSION &&
              module load gcc/\$GCC_VERSION &&
              ./tools/build_config/build.sh --test
            '''
          }
          else {
            powershell './tools/build_config/build.ps1 -test'
          }
        }
      }
    }

    stage('Package') {
      when {
        expression {
          run_build_stage == true
        }
      }
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              ./tools/build_config/build.sh --package
            '''
          }
          else {
            powershell './tools/build_config/build.ps1 -package'
          }
          // Archive a file containing the git revision being built. This
          // enables the Deploy pipelines to validate against this SHA, and
          // ensure the correct revision is tagged in Git.
          if (get_build_type(env.JOB_BASE_NAME) == 'Release') {
            def git_rev_file_name = "${env.JOB_BASE_NAME}-git-revision.sha"
            write_git_revision_to_file(git_rev_file_name)
            archiveArtifacts(artifacts: git_rev_file_name, fingerprint: true)
          }
        }

        // Archive the release package
        archiveArtifacts(
          artifacts: 'build/Herbert-*',
          fingerprint: true
        )
      }
    }
  }

  post {
    always {
      // archive static analysis report
      archiveArtifacts(
        artifacts: '**/cppcheck.xml,**/mlint.json',
        allowEmptyArchive: true,
        fingerprint: true
      )

      recordIssues(
        tools: [
          cppCheck(pattern: '**/cppcheck.xml', reportEncoding: 'UTF-8'),
          issues(id: 'mlint', name: 'MLint', pattern: '**/mlint.json')
        ]
      )

      archiveArtifacts(
        artifacts: 'build/Testing/**/*.xml',
        allowEmptyArchive: true,
        fingerprint: true
      )
      xunit (
        testTimeMargin: '3000',
        thresholdMode: 1,
        thresholds: [
          skipped(failureThreshold: '0'),
          failed(failureThreshold: '0')
        ],
        tools: [
          CTest(
            pattern: 'build/Testing/**/*.xml',
            deleteOutputFiles: true,
            failIfNotNew: false,
            skipNoTestFiles: true,
            stopProcessingIfError: true
          )
        ]
      )

    }

    success {
      post_github_status("success", "The build succeeded")
    }

    unsuccessful {
      post_github_status("failure", "The build failed")
    }

    cleanup {
      deleteDir()
    }
  }
}
