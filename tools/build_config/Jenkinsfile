#!groovy

def post_github_status(String state, String message) {
  // Non-PR builds will not set PR_STATUSES_URL - in which case we do not
  // want to post any statuses to Git
  if (env.PR_STATUSES_URL) {
    script {
      withCredentials([string(credentialsId: 'GitHub_API_Token',
          variable: 'api_token')]) {
        if (isUnix()) {
          sh """
            curl -H "Authorization: token ${api_token}" \
              --request POST \
              --data '{"state": "${state}", \
                  "description": "${message}", \
                  "target_url": "$BUILD_URL", \
                  "context": "$JOB_BASE_NAME"}' \
              $PR_STATUSES_URL > /dev/null
          """
        }
        else {
          powershell """
            [Net.ServicePointManager]::SecurityProtocol = "tls12, tls11, tls"
            \$payload = @{
              "state" = "${state}";
              "description" = "${message}";
              "target_url" = "$BUILD_URL";
              "context" = "$JOB_BASE_NAME"}

            Invoke-RestMethod -URI "$PR_STATUSES_URL" \
              -Headers @{Authorization = "token ${api_token}"} \
              -Method 'POST' \
              -Body (\$payload|ConvertTo-JSON) \
              -ContentType "application/json"
          """
        }
      }
    }
  }
}

/* Required pipeline parameters:
 *   AGENT
 *   CMAKE_VERSION
 *   MATLAB_VERSION
 *   GCC_VERSION
 *   OPEN_MPI_VERSION
 *   RELEASE_TYPE
*/
pipeline {
  agent {
    label env.AGENT
  }

  environment {
    // This variable becomes the path returned by `tempdir` in Matlab
    // It avoids any collisions between jobs when writing temporary files
    TMP = "${env.WORKSPACE}/temp"
  }

  stages {

    stage('Notify') {
      steps {
        script {
          if (env.BRANCH_NAME) {
            currentBuild.description = "Branch: ${env.BRANCH_NAME}"
          } else if (env.PR_NUMBER) {
            currentBuild.displayName = "#${env.BUILD_NUMBER} PR-${env.PR_NUMBER}"
            currentBuild.description = "Git-SHA: ${env.PR_COMMIT_SHA.take(7)}"
          }
        }
        post_github_status("pending", "The build is running")
      }
    }

    stage('Build') {
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              module load matlab/\$MATLAB_VERSION &&
              module load gcc/\$GCC_VERSION &&
              module load openmpi/\$OPEN_MPI_VERSION &&
              ./tools/build_config/build.sh --print_versions --build \
                --cmake_flags "-DHerbert_RELEASE_TYPE=$RELEASE_TYPE"
            '''
          }
          else {
            powershell '''
              ./tools/build_config/build.ps1 -print_versions -build \
                  -cmake_flags "-DHerbert_RELEASE_TYPE=\$env:RELEASE_TYPE"
            '''
          }
        }
      }
    }

    stage('Test') {
      steps {
        script {
          // We create a temporary directory to write/read test files in
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              module load matlab/\$MATLAB_VERSION &&
              module load gcc/\$GCC_VERSION &&
              module load openmpi/\$OPEN_MPI_VERSION &&
              mkdir \$TMP &&
              ./tools/build_config/build.sh --test
            '''
          }
          else {
            powershell '''
              New-Item -ItemType Directory \$env:TMP -Force
              ./tools/build_config/build.ps1 -test
            '''
          }
        }
      }
    }

    stage('Package') {
      steps {
        script {
          if (isUnix()) {
            sh '''
              module load cmake/\$CMAKE_VERSION &&
              ./tools/build_config/build.sh --package
            '''
          }
          else {
            powershell '''
              ./tools/build_config/build.ps1 -package
            '''
          }
        }

        // Archive the release package
        archiveArtifacts(
          artifacts: 'build/Herbert-*',
          fingerprint: true
        )
      }
    }
  }

  post {
    always {
      // archive CTest results file
      archiveArtifacts(
        artifacts: 'build/Testing/**/*.xml',
        fingerprint: true
      )

      xunit (
        testTimeMargin: '3000',
        thresholdMode: 1,
        thresholds: [
          skipped(failureThreshold: '0'),
          failed(failureThreshold: '0')
        ],
        tools: [
          CTest(
            pattern: 'build/Testing/**/*.xml',
            deleteOutputFiles: true,
            failIfNotNew: false,
            skipNoTestFiles: true,
            stopProcessingIfError: true
          )
        ]
      )
    }

    success {
      post_github_status("success", "The build succeeded")
    }

    failure {
      post_github_status("failure", "The build failed")
    }

    cleanup {
      deleteDir()
    }
  }
}
